buildscript {
  ext {
    junitVersion = "4.13.2"
    androidJunitVersion = "1.1.3"

    coroutinesVersion = "1.6.0"
    hiltVersion = "2.40.5"
    roomVersion = "2.4.1"
  }

  dependencies {
    classpath "com.google.dagger:hilt-android-gradle-plugin:$hiltVersion"
  }

  repositories {
    mavenCentral()
  }
}

plugins {
  id "com.android.application" version "7.1.1" apply false
  id "com.android.library" version "7.1.1" apply false
  id "org.jetbrains.kotlin.android" version "1.6.10" apply false
  id 'org.jetbrains.kotlin.jvm' version '1.6.10' apply false
}

subprojects {
  afterEvaluate { project ->
    if (project.hasProperty("android")) {
      applyAndroid(project)
    }
  }

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs.add("-Xlint:deprecation")
    options.compilerArgs.add("-Xlint:overloads")
    options.compilerArgs.add("-Xlint:overrides")
    options.compilerArgs.add("-Xlint:unchecked")
    options.compilerArgs.add("-Werror")
  }
  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    // kotlinOptions.freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    // kotlinOptions.freeCompilerArgs += "-Xopt-in=kotlin.ExperimentalUnsignedTypes"
    // kotlinOptions.freeCompilerArgs += "-Xopt-in=kotlin.contracts.ExperimentalContracts"
    // kotlinOptions.freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
    // kotlinOptions.freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.DelicateCoroutinesApi"
    kotlinOptions.allWarningsAsErrors = true
  }
}

def applyAndroid(project) {
  project.android {
    compileSdk 31

    defaultConfig {
      minSdk 22
      targetSdk 31
    }

    compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_8
      targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
      jvmTarget = JavaVersion.VERSION_1_8
    }

    buildTypes {
      release {
        minifyEnabled true
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      }
    }
  }

  project.dependencies {
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion")

    // *** hilt
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"
    implementation "com.google.dagger:hilt-android:$hiltVersion"

    // *** hilt local testing
    kaptTest "com.google.dagger:hilt-compiler:$hiltVersion"
    testImplementation "com.google.dagger:hilt-android-testing:$hiltVersion"

    // *** hilt instrumentation testing
    kaptAndroidTest "com.google.dagger:hilt-compiler:$hiltVersion"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hiltVersion"

    // *** testing
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidJunitVersion"
  }

  project.kapt {
    correctErrorTypes true
  }
}